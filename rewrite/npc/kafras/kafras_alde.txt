//===== eAthena Script =======================================
//= Al De Baran Kafras
//===== By: ============================
//= eAthena Dev Team
//===== Current Version: =======================
//= 2.3a
//===== Compatible With: ============================
//= eAthena 1.0
//===== Description: ============================================
//= Description of argument settings for callfunc "F_Kafra".
//=  arg(0): When set at 0 the default Kafra message is displayed.
//=    When set to 1 the Niflhiem Kafra message is displayed.
//=    When set to 2 the Guild Kafra message is displayed. 
//=  arg(1): Set to 1 to disable teleport menu option. Otherwise set to 0.
//=  arg(2): Set to 1 to disable info menu.  Otherwise set to 0.
//=  arg(3): Cost of Storage service
//=  arg(4): Cost of Rent a Pushcart service
//===== Additional Comments: ==========================================
//= v1.1 Now using functions :)
//= v2.1b Minor changes to function calls. Using arguments.
//= This version uses arrays .[kobra_k88]
//= 2.2 New teleport destinations, prices [Lupus]
//= 2.3 Updated function calls with new arguments. [L0ne_W0lf]
//=	Kafra Leilah is now a full NPC and no longer uses functions.
//=	Added unique Kafra Welcome message.
//=	Changed "Yuno" to "Juno"
//= 2.3a Added "callfunc("F_CheckKafCode")" when you open storage. (bugreport:515) [Samuray22]
//=====================================================================

// Kafra Main Office (Kafra Leilah) ------------------------------------
aldeba_in,96,181,4	script	Kafra Service	113,{
	cutin "kafra_05",2;
	mes "[Kafra Leilah]";
	mes "Hm...?";
	mes "Oh, welcome to";
	mes "the Kafra Corporation";
	mes "Headquarters. Did you";
	mes "need something?";
	next;
	switch(select("Save:Use Storage:Rent a Pushcart:Cancel")) {
	case 1:
		mes "[Kafra Leilah]";
		mes "Your Respawn Point has";
		mes "been saved here, inside";
		mes "of the Kafra Corporation";
		mes "Headquarters. Thank you.";
		next;
		savepoint "aldeba_in",96,179;
		mes "[Kafra Leilah]";
		mes "Please make use of";
		mes "the Kafra Services that are";
		mes "available throughout all of";
		mes "Rune Midgard. Thank you for";
		mes "visiting the Kafra Headquarters.";
		close2;
		break;
	case 2:
		if (zeny < 20) {
			mes "[Kafra Leilah]";
			mes "Excuse me, but it";
			mes "seems that you don't";
			mes "have the 20 zeny to pay";
			mes "the Storage access fee...";
			close;
		}
		mes "[Kafra Leilah]";
		mes "Although this facility is";
		mes "exclusively intended for";
		mes "the training of Kafra Employee";
		mes "and administrative functions,";
		mes "I'll access your Storage for you.";
		next;
		set zeny,zeny-20;
		set RESRVPTS, RESRVPTS + (20/5);
		mes "[Kafra Leilah]";
		mes "In the future, please";
		mes "ask the Kafra Employee on";
		mes "duty if you wish to use";
		mes "any of the Kafra Services.";
		mes "Thank you for your patronage.";
		callfunc("F_CheckKafCode");    //check your storage password, if set
		close2;
		openstorage;
		break;
	case 3:
		mes "[Kafra Leilah]";
		mes "My apologies, but I'm";
		mes "not on duty. I'd assist you";
		mes "if I could, but actually don't";
		mes "have any available Pushcarts.";
		mes "Why don't you ask another Kafra";
		mes "Employee for assistance?";
		next;
		mes "[Kafra Leilah]";
		mes "Kafra Employees are";
		mes "stationed all over the";
		mes "Rune Midgard continent,";
		mes "and you should be able to find";
		mes "plenty outside in Al De Baran.";
		close2;
		break;
	case 4:
		mes "[Kafra Leilah]";
		mes "^666666*Whew...*^000000";
		mes "Great, because I'm";
		mes "actually on my break";
		mes "right now. Choosing";
		mes "''Cancel'' was a good";
		mes "move on your part.";
		close2;
		break;
	}
	cutin "",255;
	end;
}

// Central --------------------------------------------------
aldebaran,143,119,4	script	Kafra Employee::kaf_aldebaran	113,{
	cutin "kafra_05",2;
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	setarray @wrpP[0], 1200, 1200, 1800, 1700;
	setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	set @wrpC$[0], @wrpD$[0]+" -> "+@wrpP[0];
	set @wrpC$[1], @wrpD$[1]+" -> "+@wrpP[1];
	set @wrpC$[2], @wrpD$[2]+" -> "+@wrpP[2];
	set @wrpC$[3], @wrpD$[3]+" -> "+@wrpP[3];
	set @wrpC$[4], "Cancel";
	mes "[Kafra Employee]";
	mes "The Kafra Corporation";
	mes "is always working to provide";
	mes "you with convenient services.";
	mes "How may I be of assistance?";
	callfunc "F_Kafra",5,0,1,20,600;

	M_Save:
		savepoint "aldebaran",143,109;
		callfunc "F_KafEnd",0,1,"in the city of Al De Baran";
}
